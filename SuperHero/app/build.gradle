apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

repositories {
    mavenCentral()
}

configurations {
    ktlint
}

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "superhero.setkus.lt.superhero"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())

        def publicKey = properties.getProperty('marvel.publickey')
        def privateKey = properties.getProperty('marvel.privatekey')
        def superHeroKey = properties.getProperty('superhero.key')

        debug {
            buildConfigField "int[]", "PUBLIC_KEY", "{${publicKey}}"
            buildConfigField "int[]", "PRIVATE_KEY", "{${privateKey}}"
            buildConfigField "String", "KEY", "\"${superHeroKey}\""
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "int[]", "PUBLIC_KEY", "{${publicKey}}"
            buildConfigField "int[]", "PRIVATE_KEY", "{${privateKey}}"
            buildConfigField "String", "KEY", "\"${superHeroKey}\""
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

dependencies {
    def appDependencies = rootProject.ext.appDependencies
    def appTestDependencies = rootProject.ext.appTestDependencies

    ktlint appDependencies.ktlint

    implementation appDependencies.kotlinStdLib

    implementation appDependencies.koin

    implementation appDependencies.retrofit
    implementation appDependencies.retrofitGson
    implementation appDependencies.okHttp
    implementation appDependencies.okHttpInterceptor

    implementation appDependencies.timber

    implementation appDependencies.appCompat
    implementation appDependencies.constraintLayout

    testImplementation appTestDependencies.junit
    testImplementation appTestDependencies.kotlinStdLib
    testImplementation appTestDependencies.kotlinJunitTest

    androidTestImplementation appTestDependencies.androidTestRunner
    androidTestImplementation appTestDependencies.espressoCore
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}
check.dependsOn ktlint
